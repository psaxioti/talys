#!/bin/csh -f
unalias -a
#
# This script takes care of the TALYS installation.
# All you have to do is adapt the next 2 variables to your own situation,
# and run.....
#
set compiler='lf95'       # your own Fortran compiler
set bindir=${HOME}'/bin'  # destiny directory of the TALYS executable
#
# From here on, the rest should work automatically
#
# 1. Define the talys directory
#
set talysdir=`pwd`
#
# 2. Ensure that all directories have execute permission and that 
#    all files have a read and write permission   
#
cd ..
chmod -R u+rwX talys
#
# 3. Ensure that TALYS can read the structure database by replacing
#    the path name in subroutine machine.f
#
set datapath=${talysdir}'/structure/'
set datapathnew=`echo $datapath | sed -e 's/\//\\\//g'`
cd ${talysdir}'/source/'
sed "s/path='.*'/path='${datapathnew}'/" machine.f  > machine_tmp.f
mv -f machine_tmp.f machine.f
# 
# 4. Compile TALYS.
#    Please note that the compilation of ecis97t.f
#    may result in some trivial warning messages
#
${compiler} -c *.f
${compiler} *.o -o talys
#
# 5. Check whether the setup procedure has been successful
#
if ( -e talys ) then
 mv -f talys ${bindir}/talys
 echo ' '
 echo 'The TALYS setup has been completed.'
 echo ' '
 echo 'You will find a talys executable in your' $bindir 'directory.'
 echo ' '
 echo 'You are all set to run the sample problems in the samples directory'
 echo 'with the verify script.'
else
 echo 'TALYS setup failed'
endif
